plugins {
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = "io.github.portlek"
    version = properties.configsVersion

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://jitpack.io/'
        }
    }

    dependencies {
        implementation("io.github.portlek:reflection:3.1.2")
        implementation("com.github.Carleslc:Simple-YAML:1.6.1")
        compileOnly("org.jetbrains:annotations:19.0.0")
        compileOnly("org.projectlombok:lombok:1.18.12")
        annotationProcessor("org.projectlombok:lombok:1.18.12")
        testCompile("org.junit.jupiter:junit-jupiter:5.6.2")
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    shadowJar {
        minimize()
        setArchivesBaseName(this.name)
        archiveClassifier.set("")
        archiveVersion.set("")
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name 'configs'
                    packaging 'jar'
                    description 'Annotation based configuration library for any Java project.'
                    url 'https://github.com/portlek/configs'

                    scm {
                        connection 'scm:git:git://github.com/portlek/configs.git'
                        developerConnection 'scm:git:ssh://github.com/portlek/configs.git'
                        url 'https://github.com/portlek/configs'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id 'portlek'
                            name 'Hasan Demirta≈ü'
                            email 'hasan@infumia.com.tr'
                            roles {
                                role 'Architect'
                                role 'Developer'
                            }
                            timezone '+3'
                        }
                    }
                }
            }
        }
    }
}

task gradleVersion(type: Wrapper) {
    gradleVersion = '6.5.1'
}
